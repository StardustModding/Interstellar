plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "io.github.pacifistmc.forgix" version "1.2.6"
    id "dev.architectury.loom" version "1.2-SNAPSHOT" apply false
}

forgix {
    group = maven_group
    mergedJarName = "$mod_id-$minecraft_version-${mod_version}.jar"
    outputDir = "build/libs/merged"
    
    forge {
        projectName = "forge"
        jarLocation = "build/libs/$mod_id-forge-$minecraft_version-${mod_version}.jar"
    }
    
    fabric {
        projectName = "fabric"
        jarLocation = "build/libs/$mod_id-fabric-$minecraft_version-${mod_version}.jar"
    }
}

architectury {
    minecraft = rootProject.minecraft_version
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        mavenCentral()

        maven {
            name = "TerraformersMC Maven"
            url = "https://maven.terraformersmc.com/releases"
        }

        maven {
            name = "Shedaniel Maven"
            url = "https://maven.shedaniel.me"
        }

        maven {
            name = "WispForest Maven"
            url = "https://maven.wispforest.io"
        }

        maven {
            name = "Galacticraft Maven"
            url = "https://maven.galacticraft.net/repository/maven-releases/"
        }

        maven {
            name = "Kyrptonaught Maven"
            url = "https://maven.kyrptonaught.dev"
        }

        maven {
            name = "BadAsIntended Maven"
            url = "https://maven.bai.lol"
        }
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = "UTF-8"
            options.release = 17
        }
    }

    java {
        withSourcesJar()
    }

    build.finalizedBy(mergeJars)
    assemble.finalizedBy(mergeJars)
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"

        mappings loom.layered {
            officialMojangMappings()
            // mappings "net.fabricmc:yarn:1.19.2+build.28:v2"
        }
    }
}
